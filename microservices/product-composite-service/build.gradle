plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.3'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'se.magnus.microservices.composite.product'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

ext {
    springCloudVersion = "2025.0.0"
    resilience4jVersion = "2.3.0"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":api")
    implementation project(":util")

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security:spring-security-oauth2-resource-server'
    implementation 'org.springframework.security:spring-security-oauth2-jose'

    implementation 'org.springframework.cloud:spring-cloud-starter-stream-rabbit:4.3.0'
    implementation 'org.springframework.cloud:spring-cloud-starter-stream-kafka:4.3.0'
    implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.8.9'
    implementation 'org.springframework.cloud:spring-cloud-starter-loadbalancer'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

    // config 서버 설정
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.springframework.retry:spring-retry'

    implementation "io.github.resilience4j:resilience4j-spring-boot3:${resilience4jVersion}"
    implementation "io.github.resilience4j:resilience4j-reactor:${resilience4jVersion}"

    // Micrometer (메트릭 수집)
    implementation 'io.micrometer:micrometer-registry-prometheus'

    testImplementation 'org.springframework.cloud:spring-cloud-starter-config'
    testImplementation 'org.springframework.retry:spring-retry'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.cloud:spring-cloud-stream-test-binder:4.3.0'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

/*
    Spring Cloud - Spring boot 간에는 호환되는 버전이 있으며 그걸 맞춰 주어야 작동한다

    버전이 맞지 않는 경우
    Spring Boot [3.5.3] is not compatible with this Spring Cloud release train
    Could not find class [org.springframework.boot.thread.Threading]
    오류가 발생한다

    [호환되는 버전 정보]
    | Spring Boot | Spring Cloud |
    | 3.5.x       | 2025.0.0     |
    | 3.3.x       | 2023.0.x     |
    | 3.2.x       | 2023.0.x     |
    | 3.1.x       | 2022.0.x     |

    2025-08-09 현재 Spring Cloud 최신 버전은 2025.1.0-M1 인데 이는 Spring Boot 4.0.0-M1 과 매치가 된다
    Spring boot 3.5.x 의 경우 2025.0.0 과 호환된다 2025.1.0-M1 과는 호환이 되지 않으니 주의
 */
dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
    /*
        io.github.resilience4j 에서 정의된 의존성 버전과 하위 의존성 버전이 틀린 경우가 있다
        resilience4j-spring-boot3 2.3.0
         - resilience4j-spring6 / resilience4j-annotations / resilience4j-consumer / framework-common 전주 2.2.0 으로 정의된다

        이 경우 classNotFoundException 이 발생하기 때문에 버전을 명시적으로 동일하게 설정한다
     */
    dependencies {
        dependency "io.github.resilience4j:resilience4j-spring6:${resilience4jVersion}"
        dependency "io.github.resilience4j:resilience4j-annotations:${resilience4jVersion}"
        dependency "io.github.resilience4j:resilience4j-consumer:${resilience4jVersion}"
        dependency "io.github.resilience4j:resilience4j-core:${resilience4jVersion}"
        dependency "io.github.resilience4j:resilience4j-circuitbreaker:${resilience4jVersion}"
        dependency "io.github.resilience4j:resilience4j-ratelimiter:${resilience4jVersion}"
        dependency "io.github.resilience4j:resilience4j-retry:${resilience4jVersion}"
        dependency "io.github.resilience4j:resilience4j-bulkhead:${resilience4jVersion}"
        dependency "io.github.resilience4j:resilience4j-timelimiter:${resilience4jVersion}"
        dependency "io.github.resilience4j:resilience4j-micrometer:${resilience4jVersion}"
        dependency "io.github.resilience4j:resilience4j-circularbuffer:${resilience4jVersion}"

    }
}


tasks.named('test') {
    useJUnitPlatform()
}
